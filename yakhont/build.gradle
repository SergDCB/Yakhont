/*
 * Copyright (C) 2015-2017 akha, a.k.a. Alexander Kharitonov
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'          // Dagger 2
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'

apply from:   'preprocessor.gradle'

String verName                  = '0.9.18'
int    verCode                  =    901818

ext {
    publishedName               = 'Yakhont'
    publishedDescription        = 'The high-level Android components for data loading, location, lifecycle callbacks and many more'

    publishedGroupId            = 'com.github.akhasoft'
    publishedArtifactId         = 'yakhont'
    publishedPackaging          = 'aar'
    publishedVersion            = verName

    projectDefaultFlavor        = 'core'
    projectJavaDocLinks         = ['http://google.github.io/dagger/api/latest/'             ,
                                   'http://google.github.io/gson/apidocs/'                  ,
                                   'http://reactivex.io/RxJava/1.x/javadoc/'                ,
                                   'http://reactivex.io/RxJava/2.x/javadoc/'                ,
                                   'http://square.github.io/retrofit/1.x/retrofit/'         ,
                                   'http://square.github.io/retrofit/2.x/retrofit/']
}

//noinspection GroovyMissingReturnStatement
android {
    compileSdkVersion           25
    buildToolsVersion          '25.0.3'

    defaultConfig {
        minSdkVersion            9
        //noinspection OldTargetApi
        targetSdkVersion        23
        versionCode        verCode
        versionName        verName
    }

    compileOptions {
        sourceCompatibility     JavaVersion.VERSION_1_7
        targetCompatibility     JavaVersion.VERSION_1_7
    }

    productFlavors {
        //noinspection GroovyMissingReturnStatement
        core {
            minSdkVersion       11
        }
        support     // same as core but based on support library (android.support.v4.app.Fragment etc.)
        full        // core + support + debug classes for Activities and Fragments
    }

    defaultPublishConfig        'supportRelease'

    lintOptions {
        abortOnError            false
    }

    buildTypes {
        release {
            minifyEnabled       true

            rootProject.ext.projectProGuardFiles.each {
                //noinspection GroovyAssignabilityCheck
                proguardFile            '../proguard/libs/' + it
            }
            proguardFile                './proguard/proguard-consumer.pro'
            proguardFile                './proguard/proguard-project-lib.pro'
            proguardFile                '../proguard/proguard-project.pro'
            //noinspection GroovyAssignabilityCheck
            proguardFile                getDefaultProguardFile('proguard-android.txt')

            consumerProguardFiles       './proguard/proguard-consumer.pro'
        }
    }
}

ext.libVerSupport               = '25.3.1'
ext.libVerDagger                =   '2.10'
ext.libVerRetrofit2             =  '2.3.0'

ext.libVerOkHttp3               =  '3.8.0'
ext.libVerOkHttp                =  '2.7.5'
                                                                            // in alphabetical order:
dependencies {
    compile     "com.google.dagger:dagger:${libVerDagger}"                  // Dagger2
    apt         "com.google.dagger:dagger-compiler:${libVerDagger}"
    provided    "javax.annotation:jsr250-api:1.0"

    compile     "com.android.support:appcompat-v7:${libVerSupport}"         // Google
    compile     "com.android.support:design:${libVerSupport}"
    compile     "com.android.support:recyclerview-v7:${libVerSupport}"
    compile     "com.google.android.gms:play-services-location:11.0.4"

    fullCompile "com.android.support:leanback-v17:${libVerSupport}"
    fullCompile "com.android.support:mediarouter-v7:${libVerSupport}"
    fullCompile "com.android.support:preference-v14:${libVerSupport}"
    fullCompile "com.android.support:preference-leanback-v17:${libVerSupport}"

    compile     "com.google.code.gson:gson:2.7"                             // Gson

    compile     "com.squareup.picasso:picasso:2.5.2"                        // Picasso

    compile     "com.squareup.retrofit2:retrofit:${libVerRetrofit2}"        // Retrofit
    compile     "com.squareup.retrofit2:converter-gson:${libVerRetrofit2}"
    compile     "com.squareup.okhttp3:okhttp:${libVerOkHttp3}"
    compile     "com.squareup.okhttp3:logging-interceptor:${libVerOkHttp3}"

    compile     "com.squareup.retrofit2:adapter-rxjava:${libVerRetrofit2}"
    compile     "com.squareup.retrofit2:adapter-rxjava2:${libVerRetrofit2}"

    compile     "com.squareup.retrofit:retrofit:1.9.0"
    compile     "com.squareup.okhttp:okhttp:${libVerOkHttp}"
    compile     "com.squareup.okhttp:okhttp-urlconnection:${libVerOkHttp}"

    compile     "io.reactivex.rxjava2:rxjava:2.1.0"                         // Rx
    compile     "io.reactivex:rxjava:1.3.0"
}

apply from: '../publish.gradle'
